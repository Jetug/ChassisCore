import groovy.json.JsonSlurper

buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
plugins {
    id 'eclipse'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

version = '1.0'
group = 'com.jetug.armorpunk'
archivesBaseName = 'chassis_core'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2022.11.06-1.18.2'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'chassis_core'

            arg "-mixin.config=chassis_core.mixin.json"

            mods {
                chassis_core {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'chassis_core'

            arg "-mixin.config=chassis_core.mixin.json"

            mods {
                chassis_core {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'chassis_core'

            mods {
                chassis_core {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'chassis_core', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                chassis_core {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.1.0'
    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.18:3.0.57')

    implementation fileTree('libs') { include '*.jar' }

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    testAnnotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, "chassis_core.mixin-refmap.json"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "chassis_core",
                "Specification-Vendor"    : "chassis_coresareus",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "chassis_coresareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakClass"              : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder"              : 0,
                "MixinConfigs"            : "chassis_core.mixin.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            artifact jar
//        }
//    }
//    repositories {
//        maven {
//            url "file://${project.projectDir}/mcmodsrepo"
//        }
//    }
//}
//
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

void genAnimationNames(){
    def directoryPath = "src/main/resources/assets/chassis_core/animations"
    def animationsTag = "animations"
    def directory = new File(directoryPath)
    def files = fileTree(directory)

    if (files) {
        files.each { file ->
            if (file.isFile() && file.name.endsWith(".json")) {
                def className = getClassName(file.name)
                def json = new JsonSlurper().parse(file)
                generateClassFile(className, json[animationsTag] as Map)
            }
        }
    }
}

void genAnimations(){
    def sourceDir = "src/main/resources/assets/chassis_core/animations"
    generateResourceFile("Animations", sourceDir)
}

void genTextures(){
    def sourceDir = "src/main/resources/assets/chassis_core/textures"
    generateResourceFile("Textures", sourceDir, true)
}

void genModels(){
    def sourceDir = "src/main/resources/assets/chassis_core/geo"
    generateResourceFile("Models", sourceDir)
}

void genResourceUtilsFile() {
    def packageName = "com.jetug.generated"
    def generatedDir = "src/main/java/com/jetug/generated/"
    def directory = new File(generatedDir)

    def generatedCode = """package ${packageName};

import net.minecraft.resources.ResourceLocation;

public class ResourceUtils {
      public static ResourceLocation resourceLocation(String location){
        return new ResourceLocation("chassis_core", location);
    }
}
"""

    if (!directory.exists()) directory.mkdirs()
    new File("${generatedDir}ResourceUtils.java").text = generatedCode
}

void generateResourceFile(String className, String directoryPath, boolean uni = false) {
    def packageName = "com.jetug.generated.resources"
    def generatedDir = "src/main/java/com/jetug/generated/resources/"
    def directory = new File(generatedDir)

    def constantsSection = generateResourceConstants(directoryPath, uni)

    def generatedCode = """package ${packageName};\n
import net.minecraft.resources.ResourceLocation;
import static com.jetug.generated.ResourceUtils.*;\n
public class ${className} {\n${constantsSection}}
"""

    if (!directory.exists()) directory.mkdirs()
    new File("${generatedDir}${className}.java").text = generatedCode
}

String generateResourceConstants(String directoryPath, boolean uni) {
    def directory = new File(directoryPath)
    def files = fileTree(directory)

    def constantsSection = new StringBuilder()
    def constant = "    public static final ResourceLocation"

    if (files) {
        files.each { file ->
            def name = getConstantName(getNameWithoutExt(file.name))

            if(file.parentFile.path != directory.path && uni)
                name = "${file.parentFile.name.toUpperCase()}_${name}"

            def text = file.path
            def sequence = "chassis_core"

            def relativePath = text.substring(text.indexOf(sequence) + sequence.length()).replace("\\", "/")
            relativePath = relativePath.startsWith("/") ? relativePath.substring(1) : text

            constantsSection.append("${constant} ${name} = resourceLocation(\"${relativePath}\");\n")
        }
    }

    return constantsSection.toString()
}

static String getNameWithoutExt(String fileName){
    return fileName.replaceAll("\\..*", "")
}

static String getClassName(String fileName) {
    def className = fileName.replaceAll(/(\.[^.]+)$/, "").tokenize('_.')
    def capitalizedWords = className.collect { it.capitalize() }
    return capitalizedWords.join()
}

static void generateClassFile(String className, Map json) {
    def generatedDir = "src/main/java/com/jetug/generated/animations/"
    def packageName = "com.jetug.generated.animations"
    def constantsSection = generateConstantsSection(json)
    def generatedCode = "package ${packageName};\n\npublic class ${className} {\n${constantsSection}}"
    def dir = new File(generatedDir)

    if (!dir.exists()) dir.mkdirs()
    new File("${generatedDir}${className}.java").text = generatedCode
}

static String generateConstantsSection(Map json) {
    def constantsSection = new StringBuilder()
    def constant = "    public static final String"

    json.each { key, value ->
        constantsSection.append("${constant} ${getConstantName(key)} = \"${key}\";\n")
    }

    return constantsSection.toString()
}

static getConstantName(String name){
    return name.toUpperCase().replaceAll("\\s+|\\.", "_")
}

tasks.register('generateAnimations') {
    doFirst {
        genResourceUtilsFile()
        genAnimationNames()
        genAnimations()
        genTextures()
        genModels()
    }
}

compileJava {
    finalizedBy generateAnimations
}

